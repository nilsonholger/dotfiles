# prompt

### battery status
_BATTERY_STATUS=''

function _battery_status {
_BATTERY_STATUS=''
local -a _STATUS
_STATUS=(`pmset -g batt | tail -n +2`)
[[ "${_STATUS[3]}" =~ .*charging.* ]] || return
local _CHARGE=${_STATUS[2]/;} _COLOR
[[ ${_CHARGE/\%} -gt 0 ]] && _COLOR="%F{red}"
[[ ${_CHARGE/\%} -gt 20 ]] && _COLOR="%F{yellow}"
[[ ${_CHARGE/\%} -gt 40 ]] && _COLOR="%F{green}"
[ "${_STATUS[3]}" = "charging;" ] && _COLOR="%F{cyan}"
_BATTERY_STATUS="${_COLOR}${_STATUS[4]/\(no}@${_CHARGE}%%f "
}

$_MBP && periodic_functions+=(_battery_status)



### git status
_GIT_STATUS=''

function _git_status {
local _TMP _DIR _BRANCH
_TMP=$(git status -s -b 2>&1) && _DIR=$(basename "`git rev-parse --show-toplevel`") || _DIR='' && _GIT_STATUS=''
[ -z $_DIR ] && return
local _STASH=`git stash list | wc -l | tr -d ' '`
[[ $_TMP =~ [MADRCU].\ [^\(] ]] && _GIT_STATUS+="%F{green}#" # staged
[[ $_TMP =~ .[MADUT]\ [^\ \(] ]] && _GIT_STATUS+="%F{yellow}#" # unstaged
[[ $_TMP =~ \\?\\? ]] && _GIT_STATUS+="%F{red}#" # untracked
[[ $_TMP =~ ahead ]] && _GIT_STATUS+="%F{green}@${${_TMP/*ahead }/[,\]]*}" # ahead of remote
[ $_STASH -ne 0 ] && _GIT_STATUS+="%F{yellow}@$_STASH" # stashed
[[ $_TMP =~ behind ]] && _GIT_STATUS+="%F{red}@${${_TMP/*behind }/\]*}" # behind remote
_BRANCH="${(R)${(fR)_TMP/\#\# }/( *|...*)}" # branch
[ $_BRANCH = "HEAD" ] && _BRANCH=$(git status 2>&1) # non regular branch
[ -n $_BRANCH ] && [[ $_BRANCH =~ " rebas" ]] && _BRANCH="REBASE" # rebase
[ ! -z $_GIT_STATUS ] && _GIT_STATUS=" %F{cyan}$_DIR:$_BRANCH$_GIT_STATUS%f" # directory
}

function precmd {
_git_status
}



# prompts
PROMPT='%F{red}%m%(1j.%F{yellow}[%j].)$_GIT_STATUS %F{green}%.%(?..%F{red}%B[%?]%b)%F{yellow}%#%(2L.%B+%b.) %f'
RPROMPT='$_BATTERY_STATUS%F{cyan}%T%f'
LISTPROMPT=''
SPROMPT="%F{yellow}%R %F{white}%b-> %F{green}%r %F{white}%b? [aeNy]%f "
