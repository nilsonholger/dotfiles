# prompt

### battery status
_BATTERY_STATUS=''

function _battery_status {
local -a _STATUS
_STATUS=(`pmset -g batt | tail -n -1`)
[[ "${_STATUS[3]}" =~ .*charging.* ]] || return
local _CHARGE=${_STATUS[2]/;} _COLOR
[[ ${_CHARGE/\%} -gt 0 ]] && _COLOR="%F{red}"
[[ ${_CHARGE/\%} -gt 20 ]] && _COLOR="%F{yellow}"
[[ ${_CHARGE/\%} -gt 40 ]] && _COLOR="%F{green}"
[ "${_STATUS[3]}" = "charging;" ] && _COLOR="%F{cyan}"
_BATTERY_STATUS="${_COLOR}${_STATUS[4]/\(no}@${_CHARGE}%%f"
}

$_MBP && periodic_functions+=(_battery_status)



# git status notification
_GIT_DIR=''
_GIT_STATUS=''
_GIT_TMP=''

function _git_dir {
_GIT_TMP=$(git status -s -b 2>&1) && _GIT_DIR=$(basename `git rev-parse --show-toplevel`) || _GIT_DIR='' && _GIT_STATUS=''
}

function _git_status {
[ -z $_GIT_DIR ] && return
local _STASH=`git stash list | wc -l | tr -d ' '`
[[ $_GIT_TMP =~ [MADRCU].\   ]] && _GIT_STATUS+="%F{green}#" # staged
[[ $_GIT_TMP =~ .[MADU]\ [^\ ] ]] && _GIT_STATUS+="%F{yellow}#" # unstaged
[[ $_GIT_TMP =~ \\?\\? ]] && _GIT_STATUS+="%F{red}#" # untracked
[[ $_GIT_TMP =~ ahead ]] && _GIT_STATUS+="%F{green}@${${_GIT_TMP/*ahead }/[,\]]*}" # ahead of remote
[ $_STASH -ne 0 ] && _GIT_STATUS+="%F{yellow}@$_STASH" # stashed
[[ $_GIT_TMP =~ behind ]] && _GIT_STATUS+="%F{red}@${${_GIT_TMP/*behind }/\]*}" # behind remote
[ ! -z $_GIT_STATUS ] && _GIT_STATUS=" %F{cyan}$_GIT_DIR:${(R)${(fR)_GIT_TMP/\#\# }/( *|...*)}$_GIT_STATUS%f" # repo and branch
}

function _git_update {
_git_dir
_git_status
}

chpwd_functions+=(_git_dir)
function precmd {
[ -n $_GIT_DIR ] && _git_update
}



# prompts
PROMPT='%F{red}%m%(1j.%F{yellow}[%j].)$_GIT_STATUS %F{green}%.%(?..%F{red}%B[%?]%b)%F{yellow}%#%(2L.%B+%b.) %f'
RPROMPT='$_BATTERY_STATUS %F{cyan}%T%f'
LISTPROMPT=''
SPROMPT="%F{yellow}%R %F{white}%b-> %F{green}%r %F{white}%b? [aeNy]%f "
