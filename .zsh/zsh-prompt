# prompt

### battery status
_BATTERY_STATUS=''

function _battery_status {
_BATTERY_STATUS=''
local -a _STATUS
_STATUS=(`sysctl hw.acpi.battery`)
[ "${_STATUS[6]}" -eq "0" ] && return
local _CHARGE=${_STATUS[2]} _COLOR _TIME="`printf "%d:%.2d" $((_STATUS[4]/60)) $((_STATUS[4]%60))`"
[[ ${_CHARGE} -gt 0 ]] && _COLOR="%F{red}"
[[ ${_CHARGE} -gt 20 ]] && _COLOR="%F{yellow}"
[[ ${_CHARGE} -gt 40 ]] && _COLOR="%F{green}"
[ "${_STATUS[6]}" -eq "2" ] && _COLOR="%F{cyan}"
_BATTERY_STATUS+="${_COLOR}${_TIME/0:-01}@${_CHARGE}%%%f "
}

# only activate if 'sysctl hw.acpi.battery' available
hash sysctl 2> /dev/null && sysctl -q hw.acpi.battery > /dev/null && periodic_functions+=(_battery_status)



### git status
_GIT_STATUS=''

function _git_status {
local _TMP _DIR _BRANCH _BRANCH_STATE
_GIT_STATUS=''
[[ $(df -T $PWD | awk 'NR>1 {if ($1 ~ ":") print $2}') =~ sshfs ]] && return
_TMP=$(git status -s -b 2>&1) || return && _DIR=$(basename "`git rev-parse --show-toplevel`")
local _STASH=`git stash list | wc -l | tr -d ' '`
[[ $_TMP =~ [MADRCU].\ [^\(] ]] && _GIT_STATUS+="%F{green}#" # staged
[[ $_TMP =~ .[MADUT]\ [^\ \(] ]] && _GIT_STATUS+="%F{yellow}#" # unstaged
[[ $_TMP =~ \\?\\? ]] && _GIT_STATUS+="%F{red}#" # untracked
[[ $_TMP =~ ahead ]] && _GIT_STATUS+="%F{green}@${${_TMP/*ahead }/[,\]]*}" # ahead of remote
[ $_STASH -ne 0 ] && _GIT_STATUS+="%F{yellow}@$_STASH" # stashed
[[ $_TMP =~ behind ]] && _GIT_STATUS+="%F{red}@${${_TMP/*behind }/\]*}" # behind remote
_BRANCH="${(R)${(fR)_TMP/\#\# }/( *|...*)}" # branch
if [ $_BRANCH = "HEAD" ]
then
	_BRANCH=$(git status 2>&1) # non regular branch
	[[ $_BRANCH =~ " rebas" ]] && _BRANCH="REBASE" # rebase
	[[ $_BRANCH =~ " detached" ]] && _BRANCH="DETACHED" # detached
	_BRANCH_STATE="HEAD"
else
	_BRANCH_STATE="OK"
fi
[ $_BRANCH_STATE = "HEAD" -o ! -z $_GIT_STATUS ] && _GIT_STATUS="%F{cyan}$_DIR:$_BRANCH$_GIT_STATUS%f "
}

function precmd {
[[ $(fc -l -n -1) =~ (git|vi|fg) ]] && _git_status
}

function chpwd {
_git_status
}



# prompts
PROMPT=''
[ -n "$SSH_TTY" ] && PROMPT+='%F{red}%m ' # host if not local
PROMPT+='$_GIT_STATUS' # git prompt
PROMPT+='%F{green}%.' # pwd
PROMPT+='%(?..%F{red}%B[%?]%b)' # return code
PROMPT+='%F{yellow}%#%(2L.%B+%b.)' # shell status
PROMPT+='%(1j.%F{yellow}[%j].)' # background jobs
PROMPT+=' %f' # reset colors

RPROMPT='$_BATTERY_STATUS%F{cyan}%T%f'
LISTPROMPT=''
SPROMPT="%F{yellow}%R %F{white}%b-> %F{green}%r %F{white}%b? [aeNy]%f "
